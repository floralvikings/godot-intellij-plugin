{
    parserClass = "com.github.floralvikings.godotintellijplugin.language.gdscript.parser.GDScriptParser"
    parserUtilClass = "com.github.floralvikings.godotintellijplugin.language.gdscript.parser.GDScriptParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "GDScript"
    psiImplClassSuffix = "Impl"
    psiPackage = "com.github.floralvikings.godotintellijplugin.language.gdscript.psi"
    psiImplPackage = "com.github.floralvikings.godotintellijplugin.language.gdscript.psi.impl"

    elementTypeHolderClass = "com.github.floralvikings.godotintellijplugin.language.gdscript.psi.GDScriptTypes"
    elementTypeClass = "com.github.floralvikings.godotintellijplugin.language.gdscript.psi.GDScriptElementType"
    tokenTypeClass = "com.github.floralvikings.godotintellijplugin.language.gdscript.psi.GDScriptTokenType"

    extends(".*_expression")=expression

    tokens = [
        //region keywords
        VAR = "var"
        CONST = "const"
        ENUM = "enum"
        FUNC = "func"
        IF = "if"
        ELIF = "elif"
        ELSE = "else"
        FOR = "for"
        WHILE = "while"
        BREAK = "break"
        CONTINUE = "continue"
        PASS = "pass"
        RETURN = "return"
        MATCH = "match"
        ASSERT = "assert"
        AWAIT = "await"
        BREAKPOINT = "breakpoint"
        CLASS = "class"
        CLASS_NAME = "class_name"
        EXTENDS = "extends"
        SELF = "self"
        SIGNAL = "signal"
        STATIC = "static"
        SETGET = "setget"
        SET = "set"
        GET = "get"
        YIELD = "yield"
        TRUE = "true"
        FALSE = "false"
        NULL = "null"
        AND = "and"
        OR = "or"
        NOT = "not"
        INT = "int"
        FLOAT = "float"
        BOOL = "bool"
        VOID = "void"
        IN = "in"
        IS = "is"
        AS = "as"
        TOOL = "tool"
        MASTER = "master"
        PUPPET = "puppet"
        REMOTE = "remote"
        SYNC = "SYNC"
        MASTERSYNC = "mastersync"
        PUPPETSYNC = "puppetsync"
        REMOTESYNC = "remotesync"
        //endregion

        //region annotations
        AT_ONREADY = "@onready"
        AT_TOOL = "@tool"

        AT_EXPORT = "@export"
        AT_EXPORT_ENUM = "@export_enum"
        AT_EXPORT_FILE = "@export_file"
        AT_EXPORT_DIR = "@export_dir"
        AT_EXPORT_GLOBAL_FILE = "@export_global_file"
        AT_EXPORT_GLOBAL_DIR = "@export_global_dir"
        AT_EXPORT_MULTILINE = "@export_multiline"
        AT_EXPORT_PLACEHOLDER = "@export_placeholder"
        AT_EXPORT_RANGE = "@export_range"
        AT_EXPORT_EXP_EASING = "@export_exp_easing"
        AT_EXPORT_COLOR_NO_ALPHA = "@export_color_no_alpha"
        AT_EXPORT_NODE_PATH = "@export_node_path"
        AT_EXPORT_FLAGS = "@export_flags"
        AT_EXPORT_FLAGS_2D_RENDER = "@export_flags_2d_render"
        AT_EXPORT_FLAGS_2D_PHYSICS = "@export_flags_2d_physics"
        AT_EXPORT_FLAGS_2D_NAVIGATION = "@export_flags_2d_navigation"
        AT_EXPORT_FLAGS_3D_RENDER = "@export_flags_3d_render"
        AT_EXPORT_FLAGS_3D_PHYSICS = "@export_flags_3d_physics"
        AT_EXPORT_FLAGS_3D_NAVIGATION = "@export_flags_3d_navigation"

        AT_ICON = "@icon"
        AT_RPC = "@rpc"
        AT_MASTER = "@master"
        AT_PUPPET = "@puppet"
        AT_REMOTE = "@remote"
        AT_MASTERSYNC = "@mastersync"
        AT_PUPPETSYNC = "@puppetsync"
        AT_REMOTESYNC = "@remotesync"
        //endregion

        //region operators
        LESS = "<"
        LESS_EQUAL = "<="
        GREATER = ">"
        GREATER_EQUAL = ">="
        EQUAL_EQUAL = "=="
        BANG_EQUAL = "!="
        AMPERSAND_AMPERSAND = "&&"
        PIPE_PIPE = "||"
        BANG = "!"
        AMPERSAND = "&"
        PIPE = "|"
        TILDE = "~"
        CARET = "^"
        LESS_LESS = "<<"
        GREATER_GREATER = ">>"
        PLUS = "+"
        MINUS = "-"
        STAR = "*"
        SLASH = "/"
        PERCENT = "%"
        DOT = "."
        DOT_DOT = ".."
        EQUAL = "="
        PLUS_EQUAL = "+="
        MINUS_EQUAL = "-="
        STAR_EQUAL = "*="
        SLASH_EQUAL = "/="
        PERCENT_EQUAL = "%="
        LESS_LESS_EQUAL = "<<="
        GREATER_GREATER_EQUAL = ">>="
        AMPERSAND_EQUAL = "&="
        PIPE_EQUAL = "|="
        CARET_EQUAL = "^="
        INFER = ":="
        L_BRACKET = "["
        R_BRACKET = "]"
        L_BRACE = "{"
        R_BRACE = "}"
        L_PAREN = "("
        R_PAREN = ")"
        COMMA = ","
        SEMICOLON = ";"
        COLON = ":"
        ARROW = "->"
        UNDERSCORE = "_"
        //endregion

        LINE_BREAK = "regexp:\n"
        WHITE_SPACE = "regexp:[ \t]"
        NODE_PATH = "regexp:\$\"?[A-Za-z0-9_/.]*\"?"
        IDENTIFIER = "regexp:[A-Za-z_][A-Za-z0-9_]*"
        LINE_COMMENT = "regexp:#[^\n]*"

        //region literals
        REAL_NUMBER = "regexp:-?[0-9_]*(\.)?([0-9_]+)?([Ee][-+]?[0-9]+)?"
        BINARY_NUMBER = "regexp:0b[10_]*"
        HEXADECIMAL_NUMBER = "regexp:0x[0-9A-Fa-f_]*"

        DOUBLE_QUOTED_STRING = "regexp:\"([^\"\\]|\\.)*\""
        SINGLE_QUOTED_STRING = "regexp:'([^'\\]|\\.)*'"
        //endregion
    ]
}

root ::= (top_level_annotation end_of_statement?)* (class_name_declaration | extends_declaration)* top_level_statement*

private meta listOf ::= <<p1>> (<<p2>> <<p1>>)*

external INDEQ ::= indEq
external INDLT ::= indLt
external INDGT ::= indGt
external INDNONE ::= indNone

private top_level_statement ::= (class_var_declaration_statement
    | class_const_declaration_statement
    | signal_declaration_statement
)

top_level_annotation ::= (AT_ICON call) | AT_TOOL

extends_declaration ::= EXTENDS nested_type end_of_statement
class_name_declaration ::= CLASS_NAME id end_of_statement

private class_var_declaration_statement ::= class_var_declaration end_of_statement
class_var_declaration ::= var_export? LINE_BREAK* AT_ONREADY? LINE_BREAK* VAR id (
    (COLON(type(
        ((LINE_BREAK (block_get|block_set))+)
        | ([(EQUAL|INFER) expression])
    )))
    | (COLON set_get)
    | [(EQUAL | INFER) expression]
)

block_get ::= <<indented (GET COLON LINE_BREAK block)>>
block_set ::= SET parameters COLON LINE_BREAK block

var_export ::= export_annotation call?
private export_annotation ::= AT_EXPORT
    | AT_EXPORT_ENUM
    | AT_EXPORT_FILE
    | AT_EXPORT_DIR
    | AT_EXPORT_GLOBAL_FILE
    | AT_EXPORT_GLOBAL_DIR
    | AT_EXPORT_MULTILINE
    | AT_EXPORT_PLACEHOLDER
    | AT_EXPORT_RANGE
    | AT_EXPORT_EXP_EASING
    | AT_EXPORT_COLOR_NO_ALPHA
    | AT_EXPORT_NODE_PATH
    | AT_EXPORT_FLAGS
    | AT_EXPORT_FLAGS_2D_RENDER
    | AT_EXPORT_FLAGS_2D_PHYSICS
    | AT_EXPORT_FLAGS_2D_NAVIGATION
    | AT_EXPORT_FLAGS_3D_RENDER
    | AT_EXPORT_FLAGS_3D_PHYSICS
    | AT_EXPORT_FLAGS_3D_NAVIGATION
set_get ::= set_or_get [COMMA set_or_get]

// TODO Add function-style property declaration
set_or_get ::= (SET | GET) EQUAL id

private class_const_declaration_statement ::= class_const_declaration end_of_statement
class_const_declaration ::= CONST id [COLON type] EQUAL expression

private signal_declaration_statement ::= signal_declaration end_of_statement
signal_declaration ::= SIGNAL id parameters?
parameters ::= L_PAREN [type (COMMA type)*] R_PAREN

block ::= <<indented <<listOf script_statement (end_of_statement (&INDNONE | &INDEQ) | &INDEQ)>>>>

private script_statement ::= assign_statement
    | return_statement
    | expression_statement

return_statement ::= RETURN expression

assign_statement ::= expression assign_operator expression
assign_operator ::= EQUAL
    | PLUS_EQUAL
    | MINUS_EQUAL
    | STAR_EQUAL
    | SLASH_EQUAL
    | PERCENT_EQUAL
    | LESS_LESS_EQUAL
    | GREATER_GREATER_EQUAL
    | AMPERSAND_EQUAL
    | PIPE_EQUAL
    | CARET_EQUAL
    | INFER

expression_statement ::= expression

// region expression
expression ::= (operand | operator)+
private operand ::= array_expression
    | dictionary_expression
    | lua_dictionary_expression
    | invocation_expression
    | lambda_expression
    | string
    | multiline_string
    | id
    | REAL_NUMBER
    | BINARY_NUMBER
    | HEXADECIMAL_NUMBER
    | TRUE
    | FALSE
    | NULL
    | SELF
private operator ::= LESS
    | LESS_EQUAL
    | GREATER
    | GREATER_EQUAL
    | EQUAL_EQUAL
    | BANG_EQUAL
    | AMPERSAND_AMPERSAND
    | PIPE_PIPE
    | BANG
    | AMPERSAND
    | PIPE
    | TILDE
    | CARET
    | LESS_LESS
    | GREATER_GREATER
    | PLUS
    | MINUS
    | STAR
    | SLASH
    | PERCENT
    | DOT
    | DOT_DOT
    | IS
    | AS
    | IN
    | AND
    | OR
    | NOT
    | IF
    | ELSE

array_expression ::= id? L_BRACKET LINE_BREAK* array_argument? LINE_BREAK* (COMMA LINE_BREAK* array_argument LINE_BREAK*)* COMMA? LINE_BREAK* R_BRACKET
array_argument ::= (VAR? expression) | DOT_DOT | UNDERSCORE

dictionary_expression ::= L_BRACE LINE_BREAK* dictionary_entry? LINE_BREAK* (COMMA LINE_BREAK* dictionary_entry LINE_BREAK*)* COMMA? LINE_BREAK* R_BRACE
dictionary_entry ::= (key LINE_BREAK* (COLON LINE_BREAK* VAR? expression)?) | DOT_DOT

lua_dictionary_expression ::= L_BRACE LINE_BREAK* lua_dictionary_entry? LINE_BREAK* (COMMA LINE_BREAK* lua_dictionary_entry LINE_BREAK*)* COMMA? LINE_BREAK* R_BRACE
lua_dictionary_entry ::= (key EQUAL expression) | DOT_DOT

invocation_expression ::= id? call
private call ::= L_PAREN LINE_BREAK* expression? LINE_BREAK* (COMMA LINE_BREAK* expression LINE_BREAK*)* R_PAREN

lambda_expression ::= FUNC id? L_PAREN id? (COMMA id)* R_PAREN COLON expression
//endregion

key ::= expression
type ::= array_type | nested_type
private array_type ::= id (L_BRACKET id R_BRACKET)
private nested_type ::= id (DOT id)*

id ::= IDENTIFIER | INT | FLOAT | BOOL | VOID | NODE_PATH
string ::= SINGLE_QUOTED_STRING | DOUBLE_QUOTED_STRING
private multiline_string ::= MULTILINE_SINGLE_QUOTED_STRING | MULTILINE_DOUBLE_QUOTED_STRING

private end_of_statement ::= (LINE_BREAK |  <<eof>> | SEMICOLON+)